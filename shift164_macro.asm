//////////////////////////////////////////////////////////////////////////////
////////////     Create by S.P.S 2012(c)                 		 /////////////
//////////////////////////////////////////////////////////////////////////////
////////////                                                     /////////////
////////////     Макросы библиотеки для сдвиговых       		 /////////////
////////////     регистров 74HC164 / 74HC595.    	             /////////////
////////////                                                     /////////////
//////////////////////////////////////////////////////////////////////////////
////////////                                                     /////////////
////////////     Тут записаны все макросы необходимые для        /////////////
////////////     работы с библиоткой. Можно и свои дописать)     /////////////
////////////                                                     /////////////
//////////////////////////////////////////////////////////////////////////////
////////////                                                     /////////////
////////////     Расположение: В самом верху, перед   .DSEG      /////////////
////////////                                                     /////////////
//////////////////////////////////////////////////////////////////////////////

;--------------------------------------------------------------
; Рабочие макросы
;--------------------------------------------------------------
;Записать БАЙТ в буфер sBUF
	.MACRO	OUTsBUF				
	PUSH	R16					
	LDI		R16,@1			
	STS		sBUF+@0,R16		
	POP		R16					
	.ENDM

;ПРИМЕР: (OUTsBUF 5,0x85)
;			1	  2   3
;1 - макрос записи БАЙТА в буфер отправки
;2 - № байта в буфере (ячейки) [0..bufsize]
;3 - Данные
;--------------------------------------------------------------
;Передача содежимого РЕГИСТРА в буфер sBUF
	.MACRO	OUTRsBUF				
	STS		sBUF+@0,@1		
	.ENDM

;ПРИМЕР: (OUTRsBUF 5,R16)
;			1	   2  3
;1 - макрос записи БАЙТА в буфер отправки
;2 - № байта в буфере (ячейки) [0..bufsize]
;3 - Передаваемый регистр
;--------------------------------------------------------------
;Установить БИТ в буфере sBUF
	.MACRO	SETsBUF				
	PUSH	R16					
	LDS		R16,sBUF+@0
	ORI		R16,1<<@1
	STS		sBUF+@0,R16
	POP		R16					
	.ENDM

;ПРИМЕР: (SETsBUF 5,0)
;			1	  2 3
;1 - макрос устаноки определенного БИТА в буфере отправки в [1]
;2 - № байта в буфере (ячейки) [0..bufsize]
;3 - № бита в определенном байте буфера отправки [0..7]
;--------------------------------------------------------------
;Отчистить БИТ в буфере sBUF
	.MACRO	CLRsBUF				
	PUSH	R16					
	LDS		R16,sBUF+@0
	ANDI	R16,~(1<<@1)
	STS		sBUF+@0,R16
	POP		R16					
	.ENDM

;ПРИМЕР: (CLRsBUF 5,0)
;			1	  2 3
;1 - макрос устаноки определенного БИТА в буфере отправки в [0]
;2 - № байта в буфере (ячейки) [0..bufsize]
;3 - № бита в определенном байте буфера отправки [0..7]
;--------------------------------------------------------------
; Служебные макросы
;--------------------------------------------------------------
;SET BIT with REG
	.MACRO	SETSB
	PUSH	R16
	.if @0 < 0x20		; Low IO
	SBI	@0,@1
	.else
		.if @0<0x40		; High IO
	IN	R16,@0
	ORI	R16,1<<@1
	OUT	@0,R16
		.else			; Memory
	LDS	R16,@0
	ORI	R16,1<<@1
	STS	@0,R16
		.endif
	.endif
	POP		R16
	.ENDM
;--------------------------------------------------------------
;Clear BIT with REG
	.MACRO	CLRSB
	PUSH	R16
	.if @0 < 0x20		; Low IO
	CBI	@0,@1
	.else
		.if @0<0x40		; High IO
	IN	R16,@0
	ANDI	R16,~(1<<@1)
	OUT	@0,R16
		.else			; Memory
	LDS	R16,@0
	ANDI	R16,~(1<<@1)
	STS	@0,R16
		.endif
	.endif
	POP		R16
	.ENDM
;--------------------------------------------------------------
